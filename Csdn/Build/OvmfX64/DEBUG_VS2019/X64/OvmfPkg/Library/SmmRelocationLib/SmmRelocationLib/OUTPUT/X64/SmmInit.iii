;------------------------------------------------------------------------------ ;
; Copyright (c) 2024, Intel Corporation. All rights reserved.<BR>
; SPDX-License-Identifier: BSD-2-Clause-Patent
;
; Module Name:
;
;   SmmInit.nasm
;
; Abstract:
;
;   Functions for relocating SMBASE's for all processors
;
;-------------------------------------------------------------------------------

%include "StuffRsbNasm.inc"

extern SmmInitHandler
extern mRebasedFlag
extern mSmmRelocationOriginalAddress

global gPatchSmmInitCr3
global gPatchSmmInitCr4
global gPatchSmmInitCr0
global gPatchSmmInitStack
global gcSmmInitGdtr
global gcSmmInitSize
global gcSmmInitTemplate
global gPatchRebasedFlagAddr32
global gPatchSmmRelocationOriginalAddressPtr32

%define LONG_MODE_CS 0x38

    SECTION .data

NullSeg: DQ 0                   ; reserved by architecture
CodeSeg32:
            DW      -1                  ; LimitLow
            DW      0                   ; BaseLow
            DB      0                   ; BaseMid
            DB      0x9b
            DB      0xcf                ; LimitHigh
            DB      0                   ; BaseHigh
ProtModeCodeSeg32:
            DW      -1                  ; LimitLow
            DW      0                   ; BaseLow
            DB      0                   ; BaseMid
            DB      0x9b
            DB      0xcf                ; LimitHigh
            DB      0                   ; BaseHigh
ProtModeSsSeg32:
            DW      -1                  ; LimitLow
            DW      0                   ; BaseLow
            DB      0                   ; BaseMid
            DB      0x93
            DB      0xcf                ; LimitHigh
            DB      0                   ; BaseHigh
DataSeg32:
            DW      -1                  ; LimitLow
            DW      0                   ; BaseLow
            DB      0                   ; BaseMid
            DB      0x93
            DB      0xcf                ; LimitHigh
            DB      0                   ; BaseHigh
CodeSeg16:
            DW      -1
            DW      0
            DB      0
            DB      0x9b
            DB      0x8f
            DB      0
DataSeg16:
            DW      -1
            DW      0
            DB      0
            DB      0x93
            DB      0x8f
            DB      0
CodeSeg64:
            DW      -1                  ; LimitLow
            DW      0                   ; BaseLow
            DB      0                   ; BaseMid
            DB      0x9b
            DB      0xaf                ; LimitHigh
            DB      0                   ; BaseHigh
GDT_SIZE equ $ -   NullSeg

gcSmmInitGdtr:
    DW      GDT_SIZE - 1
    DQ      NullSeg


    DEFAULT REL
    SECTION .text

global SmmStartup

BITS 16
SmmStartup:
    mov     eax, 0x80000001             ; read capability
    cpuid
    mov     ebx, edx                    ; rdmsr will change edx. keep it in ebx.
    mov     eax, strict dword 0         ; source operand will be patched
gPatchSmmInitCr3:
    mov     cr3, eax
o32 lgdt    [cs:ebp + (gcSmmInitGdtr - SmmStartup)]
    mov     eax, strict dword 0         ; source operand will be patched
gPatchSmmInitCr4:
    or      ah,  2                      ; enable XMM registers access
    mov     cr4, eax
    mov     ecx, 0xc0000080             ; IA32_EFER MSR
    rdmsr
    or      ah, 0x00000001                    ; set LME bit
    test    ebx, 0x00100000                  ; check NXE capability
    jz      .1
    or      ah, 0x00000008                    ; set NXE bit
.1:
    wrmsr
    mov     eax, strict dword 0         ; source operand will be patched
gPatchSmmInitCr0:
    mov     cr0, eax                    ; enable protected mode & paging
    jmp     LONG_MODE_CS : dword 0      ; offset will be patched to @LongMode
@PatchLongModeOffset:

BITS 64
@LongMode:                              ; long-mode starts here
    mov     rsp, strict qword 0         ; source operand will be patched
gPatchSmmInitStack:
    and     sp, 0xfff0                  ; make sure RSP is 16-byte aligned
    ;
    ; According to X64 calling convention, XMM0~5 are volatile, we need to save
    ; them before calling C-function.
    ;
    sub     rsp, 0x60
    movdqa  [rsp], xmm0
    movdqa  [rsp + 0x10], xmm1
    movdqa  [rsp + 0x20], xmm2
    movdqa  [rsp + 0x30], xmm3
    movdqa  [rsp + 0x40], xmm4
    movdqa  [rsp + 0x50], xmm5

    add     rsp, -0x20
    call    SmmInitHandler
    add     rsp, 0x20

    ;
    ; Restore XMM0~5 after calling C-function.
    ;
    movdqa  xmm0, [rsp]
    movdqa  xmm1, [rsp + 0x10]
    movdqa  xmm2, [rsp + 0x20]
    movdqa  xmm3, [rsp + 0x30]
    movdqa  xmm4, [rsp + 0x40]
    movdqa  xmm5, [rsp + 0x50]

    StuffRsb64
    rsm

BITS 16
gcSmmInitTemplate:
    mov ebp, [cs:@L1 - gcSmmInitTemplate + 0x8000]
    sub ebp, 0x30000
    jmp ebp
@L1:
    DQ     0; SmmStartup

gcSmmInitSize: DW $ - gcSmmInitTemplate

BITS 64
global SmmRelocationSemaphoreComplete
SmmRelocationSemaphoreComplete:
    push    rax
    mov     rax, [mRebasedFlag]
    mov     byte [rax], 1
    pop     rax
    jmp     [mSmmRelocationOriginalAddress]

;
; Semaphore code running in 32-bit mode
;
BITS 32
global SmmRelocationSemaphoreComplete32
SmmRelocationSemaphoreComplete32:
    push    eax
    mov     eax, strict dword 0                ; source operand will be patched
gPatchRebasedFlagAddr32:
    mov     byte [eax], 1
    pop     eax
    jmp     dword [dword 0]                    ; destination will be patched
gPatchSmmRelocationOriginalAddressPtr32:

BITS 64
global SmmInitFixupAddress
SmmInitFixupAddress:
    lea    rax, [@LongMode]
    lea    rcx, [@PatchLongModeOffset - 6]
    mov    dword [rcx], eax

    lea    rax, [SmmStartup]
    lea    rcx, [@L1]
    mov    qword [rcx], rax
    ret
